# NMEA grammar adapted from http://www.gpsinformation.org/dale/nmea.htm
package gps

type nmeaGrammar Peg {
	nmea NMEA
	rmc RMC
	msg NMEAi
}

NMEA <- <'$' talkerId cmd '\n'> { p.nmea = NMEA{text, p.msg} }

talkerId <-	'GP' / # GOS
		'GL' / # GLONASS
		'GA' / # Galileo
		'BD' / # BeiDou
		'GN' # Generic GNSS

cmd <-	RMC { r := p.rmc; p.msg = &r } /
	unk { p.msg = &nmeaUnk{} }

# RMC - NMEA has its own version of essential gps pvt (position, velocity, time) data.
# It is called RMC, The Recommended Minimum, which will look similar to:
#
# $GPRMC,123519,A,4807.038,N,01131.000,E,022.4,084.4,230318,003.1,W*6A
# Where:
#     RMC          Recommended Minimum sentence C
#     123519       Fix taken at 12:35:19 UTC
#     A            Status A=active or V=Void.
#     4807.038,N   Latitude 48 deg 07.038' N
#     01131.000,E  Longitude 11 deg 31.000' E
#     022.4        Speed over the ground in knots
#     084.4        Track angle in degrees True
#     230318       Date - 23rd of March 2018
#     003.1,W      Magnetic Variation
#     *6A          The checksum data, always begins with *
RMC <- 'RMC'
	',' <fix>	{ p.rmc.fix = text }
	',' <status>	{ p.rmc.status = text }
	',' (<lat>	{ p.rmc.lat = text })?
	',' (<ns>	{ p.rmc.ns = text})?
	',' (<lon>	{ p.rmc.lon = text })?
	',' (<we>	{ p.rmc.we = text})?
	',' (<knots>	{ p.rmc.knots.parse(text) })?
	',' (<track>	{ p.rmc.track = text })?
	',' <date>	{ p.rmc.date = text }
	',' (<magvar>	{ p.rmc.magvar = text })?
	',' nswe?
	(','[ADN])? # don't know why this appears
	    <chksum>	{ p.rmc.chksum = text }

fix <- [0-9]+('.'[0-9]+)?
status <- [AV]
ns <- [NS]
we <- [WE]
nswe <- ns/we
lat <- [0-9]+'.'[0-9]+
lon <- [0-9]+'.'[0-9]+
knots <- [0-9]+'.'[0-9]+
track <- [0-9]+'.'[0-9]+
date <- [0-9]+
magvar <- [0-9]+'.'[0-9]+
chksum <- '*'[0-9A-F][0-9A-F]

# Unknown command
unk <- (!'\n'.)*